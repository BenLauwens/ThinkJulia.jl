[[chap19]]
== The Goodies: Syntax

One of my goals for this book has been to teach you as little Julia as possible. When there were two ways to do something, I picked one and avoided mentioning the other. Or sometimes I put the second one into an exercise.

Now I want to go back for some of the good bits that got left behind. Julia provides a number of features that are not really necessary‚Äîyou can write good code without them‚Äîbut with them you can sometimes write code that‚Äôs more concise, readable or efficient, and sometimes all three.

This chapter and the two following discuss the things I have left out in the previous chapters: 

* syntax supplements
* functions, types and macros directly available in +Base+
(((Base)))
* functions, types and macros in the Standard Library
(((Standard Library)))

=== Blocks

+begin ... end+ is used to group a number of statements. This is called a block of code.
(((begin)))((("keyword", "begin", see="begin")))(((end)))

In <<chap04>> the +@svg+ macro was introduced:

[source,julia]
----
üê¢ = Turtle()
@svg begin
    forward(üê¢, 100)
    turn(üê¢, -90)
    forward(üê¢, 100)
end
----

The macro has as argument the block of code consisting of 3 function calls.

==== Closures and +let+ blocks 

The function +funcgen+ returns an array with two anonymous functions (see next sections) which return respectively the values 1 and 2:
(((funcgen)))((("function", "programmer-defined", "funcgen", see="funcgen")))

[source,@julia-setup chap19]
----
function funcgen(i)
    res = Function[]
    while i < 3
        push!(res, ()->i)
        i += 1
    end
    res
end
----

We don't get what we expected.

[source,@julia-repl-test chap19]
----
julia> fs = funcgen(1);

julia> fs[1]()
3
julia> fs[2]()
3
----

The returned functions are _closures_ and the variable +i+ is common to both of them.
(((closures)))

A _let block_ create new variable bindings and provides a solution to our problem:
(((let block)))(((let)))((("keyword", "let", see="let")))

[source,@julia-setup chap19]
----
function funcgen(i)
    res = Function[]
    while i < 3
        let i = i
            push!(res, ()->i)
        end
        i += 1
    end
    res
end
----

Now, we get what we expected:

[source,@julia-repl-test chap19]
----
julia> fs = funcgen(1);

julia> fs[1]()
1
julia> fs[2]()
2
----

=== Functions

==== Anonymous Functions

We can define a function without specifying a name:

[source,@julia-repl-test]
----
julia> f = x -> x^2 + 2x - 1
#1 (generic function with 1 method)
julia> f = function (x)
           x^2 + 2x - 1
       end
#3 (generic function with 1 method)
----

+f+ is an _anonymous function_ which is called as a normal function. Anonymous functions are often used as an argument to another function:
(((anonymous function)))(((Plots)))((("module", "Plots", see="Plots")))(((plot)))((("function", "Plots", "plot", see="plot")))

[source,jlcon]
----
julia> using Plots

julia> plot(0.0:0.1:10.0, f)

----

<<fig19-1>> shows the output of the plotting command.

[[fig19-1]]
.Plot
image::images/fig191.svg[pdfwidth="10cm"]

==== Named Tuples

You can name the components of a tuple:

[source,@julia-repl-test]
----
julia> x = (a=1, b=1+1)
(a = 1, b = 2)
julia> x.a
1
----

This is called a _named tuple_ having the additional property that fields can be accessed by name using dot syntax +(x.a)+.
(((named tuple)))(((dot syntax)))

==== Keyword Arguments

Function arguments can also be named:

[source,@julia-repl-test]
----
julia> function plot(x, y; style="solid", width=1, color="black")
           ###
       end
plot (generic function with 1 method)
julia> plot(0:10, 0:10; style="dotted", color="blue")

----

_Keyword arguments_ in a function are specified after a semicolon in the signature.
(((;)))(((keyword arguments)))

==== +do+ Block

In <<reading_and_writing>> we had to close the file after when where done writing. This can be done automatically using a _do block_:
(((do)))((("keyword", "do", see="do")))

[source,@julia-repl-test chap19]
----
julia> data = "This here's the wattle,\nthe emblem of our land.\n"
"This here's the wattle,\nthe emblem of our land.\n"
julia> open("output.txt", "w") do fout
           write(fout, data)
       end
48
----

This is functionally equivalent to

[source,@julia-repl-test chap19]
----
julia> f = (fout) -> begin
           write(fout, data)
       end
#7 (generic function with 1 method)
julia> open(f, "output.txt", "w")
48
----

The anonymous function is used as first argument of the function +open+:
(((open)))

[source,julia]
----
function open(f::Function, args...)
    io = open(args...)
    try
        f(io)
    finally
        close(io)
    end
end
----

A +do+ block can ‚Äúcapture‚Äù variables from its enclosing scope. For example, the variable +data+ in the above example of +open...do+ is captured from the outer scope.

=== Control Flow

==== Ternary Operator

The _ternary operator_, +?:+, is an alternative to an +if-elseif+ statement, when the choice is between single expression values. 
(((?:)))((("operator", "Base", "?:", see="?:+")))((("ternary operator", see="?:")))

[source,@julia-repl-test]
----
julia> a = 150
150
julia> a % 2 == 0 ? println("even") : println("odd")
even
----

The expression before the +?+, is a condition expression. If the condition is +true+, the expression before the +:+ is evaluated, otherwise, the expression after the +:+ is evaluated.

==== Short-Circuit Evaluation

The operators +&&+ and +||+ do a _short-circuit evaluation_: in a series of boolean expressions connected by these operators, only the minimum number of expressions are evaluated as are necessary to determine the final boolean value of the entire chain.
(((&&)))(((||)))(((short-circuit evaluation)))

For example, a recursive factorial routine could be defined like this:
(((fact)))

[source,@julia-setup]
----
function fact(n::Int)
    n >= 0 || error("n must be non-negative")
    n == 0 && return 1
    n * fact(n-1)
end
----

==== Tasks (aka Coroutines)

The Fibonnaci sequence can also be generated using a _task_.
(((task)))(((Channel)))((("type", "Base", "Channel", see="Channel")))(((put!)))((("function", "Base", "put!", see="put!")))

[source,@julia-setup chap19]
----
function fib(c::Channel)
    a = 0
    b = 1
    put!(c, a)
    while true
        put!(c, b)
        (a, b) = (b, a+b)
    end
end
----

+put!+ stores values in a channel object and +take!+ reads values:
(((take!)))((("function", "Base", "take!", see="take!")))

[source,@julia-repl-test chap19]
----
julia> fib_gen = Channel(fib);

julia> take!(fib_gen)
0
julia> take!(fib_gen)
1
julia> take!(fib_gen)
1
julia> take!(fib_gen)
2
julia> take!(fib_gen)
3
----

The function +fib+ is suspended after each call to +put!+ and resumed after +take!+. For performance reasons, several values of the sequence are buffered in the channel object between the resume/suspend cycle.

A channel object can also be used as an iterator:

[source,@julia-repl-test chap19]
----
julia> for val in Channel(fib)
           print(val, " ")
           val > 20 && break
       end
0 1 1 2 3 5 8 13 21
----


=== Types

==== Primitive Types

A concrete type consisting of plain old bits, is called a _primitive type_. Unlike most languages, you can declare your own primitive types. The standard primitive types are defined in the same way:
(((primitive type)))((("keyword", "primitive type", see="primitive type")))

[source,julia]
----
primitive type Float64 <: AbstractFloat 64 end
primitive type Bool <: Integer 8 end
primitive type Char <: AbstractChar 32 end
primitive type Int64 <: Signed 64 end
----

The number in the statements specifies how many bits are required.

==== Parametric Types

Julia's type system is _parametric_: types can have parameters.

Type parameters are introduced after the name of the type, surrounded by curly braces:
(((curly braces)))

[source,@julia-setup chap19]
----
struct Point{T<:Real}
    x::T
    y::T
end
----

This defines a new parametric type, +Point{T<:Real}+, holding two "coordinates" of type +T+ which can be any type having +Real+ as supertype.

[source,@julia-repl-test chap19]
----
julia> Point(0.0, 0.0)
Point{Float64}(0.0, 0.0)
----

Not only composite types can be parametric, abstract types and primitive types can also have a type parameter.

==== Type Unions

A _type union_ is an abstract parametric type that can act as any of its argument types:
(((type union)))(((Union)))((("type", "Base", "Union", see="Union")))

[source,@julia-repl-test]
----
julia> IntOrString = Union{Int,AbstractString}
Union{Int64, AbstractString}
julia> 150 :: IntOrString
150
julia> "Julia" :: IntOrString
"Julia"
----

=== Methods

==== Parametric Methods

Method definitions can also have type parameters qualifying their signature:
(((signature)))

[source,@julia-repl-test chap19]
----
julia> isintpoint(p::Point{T}) where {T} = T === Int64
isintpoint (generic function with 1 method)
julia> p = Point(1, 2)
Point{Int64}(1, 2)
julia> isintpoint(p)
true
----

==== Function-like Objects

An arbitrary Julia object can be made ‚Äúcallable‚Äù. Such ‚Äúcallable‚Äù objects are sometimes called _functors_.
(((functor)))

[source,@julia-setup chap19]
----
struct Polynomial{R}
    coeffs::Vector{R}
end

function (p::Polynomial)(x)
    v = p.coeffs[end]
    for i = (length(p.coeffs)-1):-1:1
        v = v*x + p.coeffs[i]
    end
    v
end
----

To evaluate the polynomial, we simply have to call it:

[source,@julia-repl-test chap19]
----
julia> p = Polynomial([1,10,100])
Polynomial{Int64}([1, 10, 100])
julia> p(3)
931
----

=== Constructors

Parametric types can be explicitely or implicitely constructed:

[source,@julia-repl-test chap19]
----
julia> Point(1,2) ## implicit T ##
Point{Int64}(1, 2)
julia> Point{Int64}(1, 2) ## explicit T ##
Point{Int64}(1, 2)
julia> Point(1,2.5) ## implicit T ##
ERROR: MethodError: no method matching Point(::Int64, ::Float64)
----

Default inner and outer constructors are generated for each +T+:
(((constructor)))

[source,julia]
----
struct Point{T<:Real}
    x::T
    y::T
    Point{T}(x,y) where {T<:Real} = new(x,y)
end

Point(x::T, y::T) where {T<:Real} = Point{T}(x,y);
----

and both +x+ and +y+ have to be of the same type.

To solve this problem following outer constructor can be defined:

[source,@julia-setup chap19]
----
Point(x::Real, y::Real) = Point(promote(x,y)...);
----

The +promote+ function is detailed in the next section.
(((promote)))((("function", "Base", "promote", see="promote")))

=== Conversion and Promotion

Julia has a system for promoting arguments to a common type. This is not done automatically but can be easily extended.

==== Conversion

A value can be converted from one type to another:
(((conversion)))(((convert)))((("function", "Base", "convert", see="convert")))

[source,@julia-repl-test]
----
julia> x = 12
12
julia> typeof(x)
Int64
julia> convert(UInt8, x)
0x0c
julia> typeof(ans)
UInt8
----

We can add our own +convert+ methods:
[source,@julia-repl-test chap19]
----
julia> Base.convert(::Type{Point{T}}, x::Array{T, 1}) where {T<:Real} = Point(x...)

julia> convert(Point{Int64}, [1, 2])
Point{Int64}(1, 2)
----

==== Promotion

_Promotion_ is the conversion of values of mixed types to a single common type:
(((promotion)))(((promote)))

[source,@julia-repl-test]
----
julia> promote(1, 2.5, 3)
(1.0, 2.5, 3.0)
----

Methods for the +promote+ function are normally not directly defined, but the auxiliary function +promote_rule+ is used to specify the rules for promotion:
(((promote_rule)))((("function", "Base", "promote_rule", see="promote_rule")))

[source,julia]
----
promote_rule(::Type{Float64}, ::Type{Int32}) = Float64
----

=== Metaprogramming

Julia code can be represented as a data structure of the language itself. This allows a program to transform and generate its own code. 

==== Expressions

Every Julia program starts as a string:

[source,@julia-repl-test chap19]
----
julia> prog = "1 + 2"
"1 + 2"
----

The next step is to parse each string into an object called an _expression_, represented by the Julia type +Expr+:
(((expression)))(((Expr)))((("type", "Base", "Expr", see="Expr")))(((parse)))((("function", "Meta", "parse", see="parse")))

[source,@julia-repl-test chap19]
----
julia> ex = Meta.parse(prog)
:(1 + 2)
julia> typeof(ex)
Expr
julia> dump(ex)
Expr
  head: Symbol call
  args: Array{Any}((3,))
    1: Symbol +
    2: Int64 1
    3: Int64 2
----

The +dump+ function displays expr objects with annotations.
(((dump)))

Expressions can be constructed directly by prefixing with +:+ inside parentheses or using a quote block
(((:)))(((quote)))((("keyword", "quote", see="quote")))

[source,@julia-repl-test chap19]
----
julia>ex = quote
          1 + 2
      end;
----

==== +eval+

Julia can evaluate an expression object using +eval+:
(((eval)))((("function", "Core", "eval", see="eval")))

[source,jlcon]
----
julia> Core.eval(Main, ex)
3
----

Every module has its own +eval+ function that evaluates expressions in its scope.

[WARNING]
====
When you are using a lot of calls to the function +eval+, often this means that something is wrong. +eval+ is considered ‚Äúevil‚Äù.
====

==== Macros

Macros can include generated code in a program. A _macro_ maps a tuple of arguments directly to a compiled expression:
(((macro)))

Here is a simple macro:
(((sayhello)))((("macro", "programmer-defined", "sayhello", see="sayhello")))

[source,@julia-setup chap19]
----
macro sayhello(name)
    :( println("Hello, ", $name, "!") )
end
----

Macros are called by prefixing their name with the +@+ (at-sign). The macro call +@sayhello("World")+ is replaced by:
(((@)))

[source,julia]
----
:((Main.println)("Hello, ", "World", "!"))
----

+@macroexpand @sayhello "World"+  returns this expression which is extremely useful for debugging.
(((@macroexpand)))((("macro", "Base", "@macroexpand", see="@macroexpand")))

[NOTE]
====
Why macros?

Macros generate and include fragments of customized code during parse time, thus _before_ the full program is run.
====

==== Generated Functions

The macro +@generated+ creates specialized code for methods depending on the types of the arguments:
(((generated functions)))(((@generated)))((("macro", "Base", "@generated", see="@generated")))

[source,@julia-setup chap19]
----
@generated function square(x)
    println(x)
    :(x * x)
end
----

The body returns a quoted expression like a macro.

For the caller, the _generate function_ behaves as a regular function:

[source,@julia-repl-test chap19]
----
julia> x = square(2); # note: output is from println() statement in the body
Int64
julia> x              # now we print x
4
julia> y = square("spam");
String
julia> y
"spamspam"
----

=== Missing Values

_missing values_ can be represented via the +missing+ object, which is the singleton instance of the type +Missing+.
(((missing values)))(((missing)))(((Missing)))((("type", "Base", "Missing", see="Missing")))

Arrays containing missing values can be created like other arrays:

[source,@julia-repl-test chap19]
----
julia> a = [1, missing]
2-element Array{Union{Missing, Int64},1}:
 1
  missing
----

The element type of such arrays is +Union{Missing, T}+, with +T+ the type of the non-missing values.

Reduction functions return +missing+ when called on arrays which contain missing values

[source,@julia-repl-test chap19]
----
julia> sum(a)
missing
----

In this situation, use the skipmissing function to skip missing values:
(((skipmissing)))((("function", "Base", "skipmissing", see="skipmissing")))

[source,@julia-repl-test chap19]
----
julia> sum(skipmissing([1, missing]))
1
----


=== Calling C and Fortran Code

Julia can integrate existing C or Fortran code by making an appropriate call with +ccall+ syntax.
(((ccall)))((("function", "Base", "ccall", see="ccall")))

In <<databases>> I introduced a Julia interface to the GDBM library of database functions. The library is written in C. To close the database a function call to +close(db)+ has to be made:

[source,julia]
----
Base.close(dbm::DBM) = gdbm_close(dbm.handle)

function gdbm_close(handle::Ptr{Cvoid})
    ccall((:gdbm_close, "libgdbm"), Cvoid, (Ptr{Cvoid},), handle)
end
----

A dbm object has a field +handle+ of +Ptr{Cvoid}+ type. This field holds a C pointer that refers to the database. To close the database the C function +gdbm_close+ has to be called having as only argument the C pointer pointing to the database and no return value. Julia does this directly with the +ccall+ function having as arguments:
(((Ptr)))((("type", "Base", "Ptr", see="Ptr")))

* a tuple consisting of a symbol holding the name of the function we want to call: +:gdbm_close+ and the shared library specified as a string: +"libgdm"+,

* the return type: +Cvoid+,

* a tuple of argument types: +(Ptr{Cvoid},)+ and

* the argument values: +handle+.

The complete mapping of the GDBM library can be found as an example in the ThinkJulia sources.

=== Glossary

closure::
Function that captures variables from its defining scope.

let block::
Block allocating new variable bindings.

anonymous function::
Function defined without being given a name.

named tuple::
Tuple with named components.

keyword arguments::
Arguments identified by name instead of only by position.

+do+ block::
Syntax construction used to define and call an anonymous function which looks like a normal code block.

ternary operator::
Control flow operator taking three operands to specify a condition, an expression to be executed when the condition yields +true+ and an expression to be executed when the condition yields +false+.

short-circuit evaluation::
Evaluation of a boolean operator for which the second argument is executed or evaluated only if the first argument does not suffice to determine the value of the expression.

task (aka coroutine)::
Control flow feature that allows computations to be suspended and resumed in a flexible manner.

primitive type::
Concrete type whose data consists of plain old bits.

type union::
Abstract type which includes as objects all instances of any of its argument types.

parametric type::
Type that can be parameterized.

functor::
Type with an associated method, so that it looks callable.

conversion::
Convert a value from one type to another.

promotion::
Converting values of mixed types to a single common type

expression::
Julia type that holds a language construct.

macro::
Way to include generated code in the final body of a program.

generated functions::
Functions capable of generating specialized code depending on the types of the arguments.

missing values::
Instances that represent data points with no value.